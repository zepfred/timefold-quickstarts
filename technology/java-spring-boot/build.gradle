plugins {
    id "org.springframework.boot" version "3.2.4"
    id "io.spring.dependency-management" version "1.1.4"
    id 'org.graalvm.buildtools.native' version '0.10.1'
    id "java"
}

def timefoldVersion = "999-SNAPSHOT"

group = "org.acme"
archivesBaseName = "timefold-solver-spring-boot-school-timetabling-quickstart"
version = "1.0-SNAPSHOT"

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}


repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    implementation platform("ai.timefold.solver:timefold-solver-bom:${timefoldVersion}")

    // Spring-boot
    implementation "org.springframework.boot:spring-boot-starter-web"
    implementation "ai.timefold.solver:timefold-solver-spring-boot-starter"

    // Swagger
    implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:2.4.0"

    // Testing
    testImplementation("org.springframework.boot:spring-boot-starter-test")
    testImplementation("ai.timefold.solver:timefold-solver-benchmark")
    testImplementation("ai.timefold.solver:timefold-solver-test")
    testImplementation "io.rest-assured:rest-assured"
    testImplementation "org.awaitility:awaitility"

    // UI
    // No webjar locator; incompatible in native mode;
    // see https://github.com/spring-projects/spring-framework/issues/27619
    // and https://github.com/webjars/webjars-locator-core/issues/96
    // runtimeOnly "org.webjars:webjars-locator:0.50"
    runtimeOnly "ai.timefold.solver:timefold-solver-webui"
    runtimeOnly "org.webjars:bootstrap:5.2.3"
    runtimeOnly "org.webjars:jquery:3.6.4"
    runtimeOnly "org.webjars:font-awesome:5.15.1"
    runtimeOnly "org.webjars.npm:js-joda:1.11.0"
}

test {
    useJUnitPlatform()
    // Log the test execution results.
    testLogging {
        events "passed", "skipped", "failed"
    }
}

// optimizedLaunch disables the C2 compiler, which has a massive performance impact
tasks.named("bootRun") {
    optimizedLaunch = false
}
